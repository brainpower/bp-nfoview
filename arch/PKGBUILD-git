# Maintainer: oi_wtf <brainpower at mailbox dot org>

pkgname="bp-nfoview-git"
pkgver=0.1.2.r0.g4efa5ff
pkgrel=1
pkgdesc="A Simple NFO-Viewer by brainpower"
arch=('i686' 'x86_64')
url="https://github.com/brainpower/bp-nfoview"
license=('GPL')
depends=('qt5-base')
makedepends=('cmake' 'git')
source=("${pkgname}::git://github.com/brainpower/bp-nfoview.git")
sha256sums=('SKIP')

pkgver(){
	cd "$srcdir/$pkgname"
	git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

build(){
  cd "${srcdir}/${pkgname}"

  mkdir -p build/
  cd build/

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make ${MAKEFLAGS}
}

package(){
  cd "${srcdir}/${pkgname}/build"
	make install DESTDIR="${pkgdir}"
}
